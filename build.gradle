/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */
import org.opensearch.gradle.test.RestIntegTestTask

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.4.0-SNAPSHOT")
        version_qualifier = System.getProperty("build.version_qualifier", "")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
            plugin_no_snapshot += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_group = "org.opensearch"
        opensearch_no_snapshot = opensearch_build.replace("-SNAPSHOT","")
    }
    repositories {
        mavenLocal()
        maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "${opensearch_group}.gradle:build-tools:${opensearch_version}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:8.0.0"
    }
}

allprojects {
    group = 'org.opensearch.plugin'
    version = opensearch_version.tokenize('-')[0] + '.0'
    if (buildVersionQualifier) {
        version += "-${buildVersionQualifier}"
    }
    if (isSnapshot) {
        version += "-SNAPSHOT"
    }

    plugins.withId('java') {
        targetCompatibility = JavaVersion.VERSION_21
        sourceCompatibility = JavaVersion.VERSION_21
    }
}

repositories {
    mavenLocal()
    maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.java-agent'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'opensearch.pluginzip'
apply from: 'gradle/formatting.gradle'

opensearchplugin {
    description = 'Plugin for TSDB'
    classname = 'org.opensearch.tsdb.TSDBPlugin'
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

publishing {
    repositories {
        maven {
            name = "Snapshots"
            url = System.getenv("MAVEN_SNAPSHOTS_S3_REPO")
            credentials(AwsCredentials) {
                accessKey = System.getenv("AWS_ACCESS_KEY_ID")
                secretKey = System.getenv("AWS_SECRET_ACCESS_KEY")
                sessionToken = System.getenv("AWS_SESSION_TOKEN")
            }
        }
    }
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
                name = "tsdb"
                description = "OpenSearch plugin for TSDB"
                groupId = "org.opensearch.plugin"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "OpenSearch"
                        // TODO to add
                        url = "https://github.com/opensearch-project/opensearch-tsdb"
                    }
                }
            }
        }
    }
}

spotless {
  java {
    licenseHeaderFile 'formatter/license-header.txt'
    targetExclude '**/package-info.java'
  }
}

// This requires an additional Jar not published as part of build-tools
loggerUsageCheck.enabled = false

afterEvaluate {
    project.validateNebulaPom.enabled = false
}

// Configure javaRestTest source set
sourceSets {
    javaRestTest {
        java {
            srcDirs = ['src/javaRestTest/java']
        }
        resources {
            srcDirs = ['src/javaRestTest/resources']
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }

    internalClusterTest {
        java {
            srcDirs = ['src/internalClusterTest/java']
        }
        resources {
            srcDirs = ['src/internalClusterTest/resources']
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    javaRestTestImplementation.extendsFrom testImplementation
    javaRestTestRuntimeOnly.extendsFrom testRuntimeOnly

    internalClusterTestImplementation.extendsFrom testImplementation
    internalClusterTestRuntimeOnly.extendsFrom testRuntimeOnly

    // Resolve dependency conflicts
    all {
        resolutionStrategy {
            force 'org.yaml:snakeyaml:2.5'
        }
    }
}

task integTest(type: RestIntegTestTask) {
    description = "Run tests against a cluster"
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

integTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

task javaRestTest(type: RestIntegTestTask) {
    description = "Run REST integration tests"
    group = JavaBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.javaRestTest.output.classesDirs
    classpath = sourceSets.javaRestTest.runtimeClasspath
    mustRunAfter test
}

javaRestTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

task internalClusterTest(type: RestIntegTestTask) {
    description = "Run internal cluster integration tests"
    group = JavaBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.internalClusterTest.output.classesDirs
    classpath = sourceSets.internalClusterTest.runtimeClasspath
    mustRunAfter test, javaRestTest
}

internalClusterTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

String javaccGrammarPath = 'src/main/java/org/opensearch/tsdb/lang/m3/m3ql/parser'
String javaccOutputPath = 'src/main/java/org/opensearch/tsdb/lang/m3/m3ql/parser/generated'

javadoc {
  source = sourceSets.main.allJava + fileTree(javaccOutputPath)
  options.addStringOption('Xdoclint:all,-missing', '-Xwerror')
  exclude '**/generated/**'
  dependsOn('generateJavaCC')
}


def numNodes = findProperty('numNodes') as Integer ?: 1
def numZones = findProperty('numZones') as Integer ?: 1

testClusters.integTest {
    testDistribution = "ARCHIVE"

    if (numZones > 1) numberOfZones = numZones
    if (numNodes > 1) numberOfNodes = numNodes

    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)
}

testClusters.javaRestTest {
    testDistribution = "ARCHIVE"

    if (numZones > 1) numberOfZones = numZones
    if (numNodes > 1) numberOfNodes = numNodes

    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)
}

testClusters.internalClusterTest {
    testDistribution = "ARCHIVE"

    if (numZones > 1) numberOfZones = numZones
    if (numNodes > 1) numberOfNodes = numNodes

    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)
}

run {
    useCluster testClusters.integTest
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/generated/**'
            ])
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)
test.finalizedBy(project.tasks.jacocoTestCoverageVerification)

jacocoTestCoverageVerification {
    dependsOn compileJava, processResources
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/generated/**'
            ])
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}


tasks.named("precommit").configure { dependsOn("spotlessCheck", "javadoc") }
tasks.named("check").configure { dependsOn(jacocoTestCoverageVerification) }

sourceSets {
    jmh {
        java.srcDirs = ['src/jmhTest/java']
        resources.srcDirs = ['src/jmhTest/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath = sourceSets.main.runtimeClasspath
    }
}

/**********************************************
 *            JavaCC Configuration            *
 **********************************************/

configurations {
    javacc
}

dependencies {
    javacc 'net.java.dev.javacc:javacc:7.0.13'
    implementation "${opensearch_group}.plugin:store-subdirectory:${opensearch_version}"

    // Jackson dependencies for YAML and JSON processing in test framework
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'

    // javaRestTest dependencies
    javaRestTestImplementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    javaRestTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    javaRestTestImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    javaRestTestImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'

    // jmh dependencies
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.35'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.35'

    // internalClusterTest dependencies
    internalClusterTestImplementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    internalClusterTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    internalClusterTestImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    internalClusterTestImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'
}


task jmh(type: JavaExec, dependsOn: [jmhClasses]) {
    main = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.output + sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

compileJmhJava {
    options.annotationProcessorPath = configurations.jmhAnnotationProcessor
    options.compilerArgs = ['-processor' ,'org.openjdk.jmh.generators.BenchmarkProcessor']
}

classes.finalizedBy(jmhClasses)

tasks.register("cleanJavaCCGenerated", Delete) {
    delete fileTree(javaccOutputPath) {
        include '*.java'
    }
}

tasks.register("generateJavaCC", JavaExec) {
    dependsOn "cleanJavaCCGenerated", "spotlessApply", "spotlessCheck"
    mainClass = 'javacc'
    classpath = configurations.javacc
    systemProperty 'file.encoding', 'UTF-8'
    args '-OUTPUT_DIRECTORY=' + javaccOutputPath,
            "${file(javaccGrammarPath)}/m3ql.jj"
}

compileJava.dependsOn generateJavaCC

// Exclude store-subdirectory from dependency license checks since it's an internal OpenSearch plugin
tasks.named("dependencyLicenses").configure {
    ignoreSha('store-subdirectory')
}

// Exclude generated files from forbidden APIs check
forbiddenApisMain {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
}

forbiddenApisTest {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
}

forbiddenApisJavaRestTest {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
}

forbiddenApisInternalClusterTest {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
}

forbiddenPatterns {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
}
